/* Copyright (c) 2023, Sascha Willems
 *
 * SPDX-License-Identifier: MIT
 *
 */

#version 460
#extension GL_EXT_ray_tracing : enable

struct RayPayload {
	vec3 color;
	float distance;
	vec3 normal;
	float reflector;
};

layout(binding = 0, set = 0) uniform accelerationStructureEXT topLevelAS;
layout(binding = 1, set = 0, rgba8) uniform image2D image;
layout(binding = 2, set = 0) uniform CameraProperties 
{
	mat4 viewInverse;
	mat4 projInverse;
	uint frame;
} cam;
layout(location = 0) rayPayloadEXT RayPayload rayPayload;
layout(location = 3) rayPayloadEXT uint payloadSeed;

void main() 
{
	const vec2 pixelCenter = vec2(gl_LaunchIDEXT.xy) + vec2(0.5);	// pixel position
	const vec2 inUV = pixelCenter/vec2(gl_LaunchSizeEXT.xy);	// pixel position in WdC
	vec2 d = inUV * 2.0 - 1.0;	// pixel position in NDC

	vec4 origin = cam.viewInverse * vec4(0.0f, 0.0f, 0.0f, 1.0f);
	vec4 target = cam.projInverse * vec4(d.x, d.y, 1.0f, 1.0f) ;
	vec4 direction = cam.viewInverse * vec4(normalize(target.xyz), 0.0f) ;

	float tmin = 0.001f;
	float tmax = 10000.0f;

	const int iterations = 3;

	vec3 color = vec3(0.0f);
	vec3 statueTex = vec3(0.0f);
	bool reflectorFlag = false;
	for (int iter = 0; iter < iterations; iter++) {
		traceRayEXT(topLevelAS, gl_RayFlagsOpaqueEXT, 0xff, 0, 0, 0, origin.xyz, tmin, direction.xyz, tmax, 0);
		vec3 hitColor = rayPayload.color;

		if (rayPayload.distance < 0.0f) {	// miss shader 얯介 썐절
			color += hitColor;
			break;
		} 
		else if (rayPayload.reflector == 1.0f) {	// chit shader 얯介얙온 reflector 첇절
			statueTex = hitColor;
			reflectorFlag = true;

			const vec4 hitPos = origin + direction * rayPayload.distance;
			origin.xyz = hitPos.xyz + rayPayload.normal * 0.001f;
			direction.xyz = reflect(direction.xyz, rayPayload.normal);
		} 
		else {	// chit shader 얯介얙온 reflector 쮅얦절
			color += hitColor;
			break;
		}
	}

	if (reflectorFlag) {
		color = mix(color, statueTex, 0.7);
	}

	imageStore(image, ivec2(gl_LaunchIDEXT.xy), vec4(color, 1.0f));


}
